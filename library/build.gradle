buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.2.5'
        classpath 'org.robolectric:robolectric-gradle-plugin:0.14.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'robolectric'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        warningsAsErrors true
    }
}

robolectric {
    include '**/*Test.class'

    afterTest { descriptor, result ->
        println "Executed test for ${descriptor.name} with result: ${result.resultType}"
    }
}

dependencies {
    compile 'com.google.android.gms:play-services-base:6.5.87'

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile('org.robolectric:robolectric:2.4') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
}

publish {
    groupId = 'com.meiste.greg'
    artifactId = 'gcm'
    version = rootProject.ext.versionName
    uploadName = 'gcm'
    description = 'GCM helper using the GoogleCloudMessaging API. Inspired by Google\'s deprecated GCMRegistrar.'
    website = 'https://github.com/meisteg/gcm'
    autoPublish = true
    dryRun = false
}

if (project.hasProperty('bintray_user') && project.hasProperty('bintray_api_key')) {
    publish.bintrayUser = bintray_user
    publish.bintrayKey = bintray_api_key
}
